plugins {
    id 'groovy'
    id 'project-report'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.0.5.RELEASE'
}

group 'com.jakemarsden'
version '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.5.RELEASE'
    }

    dependencies {
        dependencySet(group: 'org.hibernate', version: '5.3.6.Final') {
            entry 'hibernate-core'
            entry 'hibernate-java8'
        }
    }
}

dependencies {
    runtime group: 'com.h2database', name: 'h2'
    compile group: 'org.apache.commons', name: 'commons-lang3'
    compile group: 'org.codehaus.groovy', name: 'groovy-all'
    runtime group: 'org.hibernate', name: 'hibernate-java8'
    runtime group: 'org.springframework.boot', name: 'spring-boot-devtools'
    runtime group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
}

sourceSets {
    main.java.srcDirs = []
    test.java.srcDirs = []
}

springBoot {
    mainClassName = 'com.jakemarsden.aikidotrack.AikidoTrackApplication'
    buildInfo {
        properties {
            time = null
        }
    }
}

bootRun {
    // Override webapp location to use webpack output
    //   Note: trailing slash is significant
    final path = 'file:../webapp/dist/'
    args = ["--spring.resources.static-locations=$path"]
}

wrapper {
    gradleVersion = '4.10.2'
    distributionType = 'ALL'
}
