plugins {
    id 'groovy'
    id 'project-report'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
}

final os = System.getProperty('os.name').toLowerCase()
final webappDir = "$projectDir/../webapp"

// Execute tasks for production using "./gradlew <tasks> -Penv=production"
final isForProd = findProperty('env') == 'production'

group 'com.jakemarsden'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.0.3.RELEASE'
    }

    dependencies {
        dependencySet(group: 'org.hibernate', version: '5.3.1.Final') {
            entry 'hibernate-core'
            entry 'hibernate-java8'
        }
    }
}

dependencies {
    runtime group: 'com.h2database', name: 'h2'
    compile group: 'org.apache.commons', name: 'commons-lang3'
    compile group: 'org.codehaus.groovy', name: 'groovy-all'
    runtime group: 'org.hibernate', name: 'hibernate-java8'
    runtime group: 'org.springframework.boot', name: 'spring-boot-devtools'
    runtime group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
}

task compileWebapp(type: Exec) {
    inputs.property 'isForProd', isForProd
    inputs.file "$webappDir/package-lock.json"
    inputs.file "$webappDir/webapp.config.js"
    inputs.dir "$webappDir/src"
    outputs.dir "$webappDir/dist"

    executable = os.contains('windows') ? 'npm.cmd' : 'npm'
    args = ['run', isForProd ? 'build-for-prod' : 'build']
    workingDir webappDir
}

sourceSets {
    main {
        java.srcDirs = []
        output.dir "$webappDir/dist", builtBy: compileWebapp
    }
    test.java.srcDirs = []
}

bootRun {
    // Includes live reloading of webapp build output so rebuilding the webapp (ie. "npm run build") and refreshing the
    // browser will give the updated content immediately
    sourceResources sourceSets.main
}

springBoot {
    mainClassName = 'com.jakemarsden.aikidotrack.AikidoTrackApplication'
    buildInfo {
        properties {
            time = null
        }
    }
}

wrapper {
    gradleVersion = '4.7'
    distributionType = 'ALL'
}
